globals [ ant-dispatch-table NO_VAL visited ]

breed [ ants ant ]

undirected-link-breed [ connectors connector ]

ants-own [ my-sat-2d
           
           my-sentinel
           
           gps
           
           results
           
           name
           
           is-dispatched?
           
           is-waiting?
           
           time-dispatched 
           
           previous-sentinel
           
           previous-sat
           
           ;NEW
           my-config-index
           
           my-config
           
           my-sat
           
           info-visited-sentinels
           
           info-visited-sats
           
           info-state-times 
           
           info-visited-configs
           
           info-detected-probs ]

;test comment

;static procedure
to setup-ants 
  set visited []
  set NO_VAL "NO_VAL"
  create-ants ant-num [
    setup-basic-ant "freddy"
  ]
end

to setup-basic-ant [a-name]
  set name a-name
  set shape "bug"
  set color yellow
  set zcor 49
  set is-dispatched? false
  set is-waiting? false
  set info-visited-sentinels []
  set info-visited-sats []
  set info-state-times array:from-list [0 0 0]
  set info-visited-configs []
  set info-detected-probs 0
  ;this will always return a "nobody" value before properly assigned.
  set previous-sentinel sentinel -1
  set results []   
end

;test for potential problem
to go-ant
  let new-list []
  ask my-sat [
    if has-problem? [my-config] of myself [
      set new-list (lput (list lat long) [results] of myself)
    ]
  ]
  set results new-list
end

to draw-ant
  ifelse is-dispatched? [
    ifelse display-in-3d? [ 
      set xcor [xcor] of my-sat
      set ycor [ycor] of my-sat
      set zcor [zcor] of my-sat
      if is-dispatched? [
        create-connector-with [my-ground] of my-sentinel
      ]
    ]
    [
      set xcor [xcor] of my-sat-2d
      set ycor [ycor] of my-sat-2d
      set zcor [zcor] of my-sat-2d
      if is-dispatched? [
        create-connector-with my-sentinel
      ]
    ]
  ]
  [
    ask my-connectors [ die ]
    ifelse display-in-3d? [
      set xcor [xcor] of [my-ground] of my-sentinel
      set ycor [ycor] of [my-ground] of my-sentinel
      set zcor [zcor] of [my-ground] of my-sentinel
    ]
    [
      set xcor [xcor] of my-sentinel
      set ycor [ycor] of my-sentinel
      set zcor [zcor] of my-sentinel
    ]
  ]
end

to move-ant
  ; to-do
  ; maybe don't move?
  ;set my-sentinel NO_VAL
  ;set my-system NO_VAL
  choose-sentinel
  choose-sat
  choose-config
end

to choose-sentinel
  let spinner []
  ask sentinels with [(self != [previous-sentinel] of myself) and participating and (table:length ant-queues > 0)] [
    foreach n-values (pheromones + 1) [?] [
      set spinner lput self spinner 
    ] 
  ]
  let selected-sentinel item (random (length spinner)) spinner
  set my-sentinel selected-sentinel
  set previous-sentinel my-sentinel
  set info-visited-sentinels lput my-sentinel info-visited-sentinels
  ;show (word "ant " self " chose " selected-sentinel)
end

to choose-sat
  let spinner []
  ask my-sentinel [
    foreach table:keys sat-sum-pheromones [
      let current-sat sat ?
      let current-sum (table:get sat-sum-pheromones ?) + 1
      foreach n-values current-sum [?] [
        set spinner lput current-sat spinner 
      ]  
    ]
  ]
  let selected-sat item (random (length spinner)) spinner
  set my-sat selected-sat
  set previous-sat my-sat
  set info-visited-sats lput my-sat info-visited-sats
  ;show (word "ant " self " chose " selected-sat)  
end

to choose-config
  ;show my-sentinel
  ;show my-sat
  let spinner []
  ;current pheromone values for this sat.
  let config-pheromone-array table:get [sat-config-pheromones] of my-sentinel [who] of my-sat
  ;show array:to-list config-pheromone-array
  foreach n-values array:length config-pheromone-array [?] [
    let current-index ?
    let current-pheromones array:item config-pheromone-array current-index
    foreach n-values (current-pheromones + 1) [?] [
      set spinner lput current-index spinner 
    ]
  ]
  
  set my-config-index item (random (length spinner)) spinner
  
  set my-config item my-config-index [available-configs] of my-sat
  
  set info-visited-configs lput my-config info-visited-configs
  
  show (word "ant " self " chose config " table:get problem-identifiers my-config " of " my-sat)
  
  ;update the ant queues of our sat.
  let existing-queue table:get [ant-queues] of my-sentinel ([who] of my-sat)
  let new-queue lput who existing-queue
  table:put ([ant-queues] of my-sentinel) ([who] of my-sat) new-queue
  set is-waiting? true
end

to report-ant
  let answer 0
  ask my-sentinel [
    ;communicate with ant
    set answer validate-report [my-config] of myself [results] of myself [my-sat] of myself myself
  ]
  set results []
  if answer > 0 [
    ;show (word "ant " self " is dropping pheromones.")
    drop-pheromones answer
  ]
end

to drop-pheromones [concentration]
  let sat-node 0
  set sat-node [who] of my-sat
  ask my-sentinel [
     ;NEW
     let existing-pheromones array:item (table:get sat-config-pheromones sat-node) [my-config-index] of myself
     array:set (table:get sat-config-pheromones sat-node) [my-config-index] of myself (existing-pheromones + concentration)
  ]
  reset-time (sat sat-node) my-config-index
end